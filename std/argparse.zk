#-
Written by Shahryar Ahmad
12 Dec 2023
-#
namespace argparse
{
    function parse(var pattern)
    {
        var argv = clone(argv)
        var parsed = {}
        for(var i = 0 to len(argv)-1)
        {
            var curr_option = argv[i]
            if(len(curr_option) == 1 or curr_option[0] !="-")
                continue
            curr_option = curr_option.erase(0,0)
            foreach(var opt: curr_option)
            {
                var idx = pattern.find(opt)
                if(idx == nil)
                    throw NameError(format("Unknown command line option '%'",opt))
                if(idx + 1 < len(pattern) and pattern[idx+1] == ":")
                {
                    if(len(curr_option) != 1 or i+1 >= len(argv))
                        throw ValueError("Syntax Error")
                    parsed.emplace(opt,argv[i+1])
                    i+=1
                }
                else
                    parsed.emplace(opt,true)
            }
        }
        return parsed
    }
}
